apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "org.ghostsinthelab.apps.guilelessbopomofo"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 19
        versionName "1.2.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
        vectorDrawables {
            useSupportLibrary = true
        }
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }
    ndkVersion '21.3.6528147'

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    task prepareChewing(type: Exec) {
        onlyIf {
            files("$rootDir/app/libs/libchewing/data/dictionary.dat", "$rootDir/app/libs/libchewing/data/index_tree.dat").any { file ->
                !file.exists()
            }
        }
        workingDir("$rootDir/app/libs/libchewing")
        commandLine('./build-android.sh')
    }

    task copyChewingDataFiles(type: Copy) {
        from("$rootDir/app/libs/libchewing/data") {
            include("dictionary.dat", "index_tree.dat", "pinyin.tab", "swkb.dat", "symbols.dat")
        }
        into("$rootDir/app/src/main/assets")
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    preBuild.dependsOn('prepareChewing', 'copyChewingDataFiles')
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'com.google.android:flexbox:2.0.1'
    implementation 'org.greenrobot:eventbus:3.2.0'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.4.21'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1'
    testImplementation 'junit:junit:4.13.1'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.5'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-accessibility:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.3.0'
    androidTestImplementation 'androidx.test.espresso.idling:idling-concurrent:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-idling-resource:3.3.0'
    androidTestImplementation 'androidx.test:core:1.3.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}
