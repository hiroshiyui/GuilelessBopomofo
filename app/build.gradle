apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.kotlin.android'

android {
    compileSdkVersion = 33
    buildToolsVersion = '33.0.2'
    namespace 'org.ghostsinthelab.apps.guilelessbopomofo'

    defaultConfig {
        applicationId "org.ghostsinthelab.apps.guilelessbopomofo"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 101
        versionName "1.9.31"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
        vectorDrawables {
            useSupportLibrary = true
        }
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1"
        }
    }

    ndkVersion '23.2.8568313'

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_17
        sourceCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    task prepareChewing(type: Exec) {
        onlyIf {
            files("$rootDir/app/libs/libchewing/data/dictionary.dat", "$rootDir/app/libs/libchewing/data/index_tree.dat").any { file ->
                !file.exists()
            }
        }
        workingDir("$rootDir/app/libs/libchewing")
        commandLine('./build-android.sh')
    }

    def chewingDataFiles = ["dictionary.dat", "index_tree.dat", "pinyin.tab", "swkb.dat", "symbols.dat"]

    task copyChewingDataFiles(type: Copy) {
        dependsOn 'prepareChewing'
        from("$rootDir/app/libs/libchewing/data") {
            include(chewingDataFiles)
        }
        into("$rootDir/app/src/main/assets")
    }

    preBuild.dependsOn('copyChewingDataFiles')

    task cleanChewingDataFiles(type: Delete) {
        chewingDataFiles.each {
            file("$rootDir/app/src/main/assets/$it").delete()
        }
    }

    task execMakeClean(type: Exec) {
        onlyIf { file("$rootDir/app/libs/libchewing/Makefile").exists() }
        workingDir("$rootDir/app/libs/libchewing")
        commandLine('make', 'clean')
        ignoreExitValue true
    }

    clean.dependsOn('cleanChewingDataFiles', 'execMakeClean')
}

dependencies {
    def androidx_appcompat_version = '1.6.1'
    def androidx_emoji2_version = '1.3.0'
    def lifecycle_version = '2.6.1'
    def kotlin_version = '1.8.22'

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "androidx.appcompat:appcompat-resources:$androidx_appcompat_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'com.github.google:flexbox-layout:2.0.1'
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
    implementation "androidx.emoji2:emoji2:$androidx_emoji2_version"
    implementation "androidx.emoji2:emoji2-views:$androidx_emoji2_version"
    implementation "androidx.emoji2:emoji2-views-helper:$androidx_emoji2_version"
    implementation "androidx.emoji2:emoji2-bundled:$androidx_emoji2_version"
    implementation "org.greenrobot:eventbus:3.3.1"
    testImplementation 'junit:junit:4.13.2'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-accessibility:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.5.1'
    androidTestImplementation 'androidx.test.espresso.idling:idling-concurrent:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-idling-resource:3.5.1'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}
