apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 32
    buildToolsVersion '32.0.0'
    namespace 'org.ghostsinthelab.apps.guilelessbopomofo'

    defaultConfig {
        applicationId "org.ghostsinthelab.apps.guilelessbopomofo"
        minSdkVersion 23
        targetSdkVersion 32
        versionCode 65
        versionName "1.8.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
        vectorDrawables {
            useSupportLibrary = true
        }
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1"
        }
    }

    ndkVersion '23.1.7779620'

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    task prepareChewing(type: Exec) {
        onlyIf {
            files("$rootDir/app/libs/libchewing/data/dictionary.dat", "$rootDir/app/libs/libchewing/data/index_tree.dat").any { file ->
                !file.exists()
            }
        }
        workingDir("$rootDir/app/libs/libchewing")
        commandLine('./build-android.sh')
    }

    def chewingDataFiles = ["dictionary.dat", "index_tree.dat", "pinyin.tab", "swkb.dat", "symbols.dat"]

    task copyChewingDataFiles(type: Copy) {
        dependsOn 'prepareChewing'
        from("$rootDir/app/libs/libchewing/data") {
            include(chewingDataFiles)
        }
        into("$rootDir/app/src/main/assets")
    }

    preBuild.dependsOn('copyChewingDataFiles')

    task cleanChewingDataFiles(type: Delete) {
        chewingDataFiles.each {
            file("$rootDir/app/src/main/assets/$it").delete()
        }
    }

    task execMakeClean(type: Exec) {
        onlyIf { file("$rootDir/app/libs/libchewing/Makefile").exists() }
        workingDir("$rootDir/app/libs/libchewing")
        commandLine('make', 'clean')
        ignoreExitValue true
    }

    clean.dependsOn('cleanChewingDataFiles', 'execMakeClean')
}

dependencies {
    def androidx_appcompat_version = '1.4.2'
    def androidx_emoji_version = '1.2.0-alpha03'

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "androidx.appcompat:appcompat-resources:$androidx_appcompat_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'com.github.google:flexbox-layout:2.0.1'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.7.10'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.3'
    implementation "androidx.emoji:emoji:$androidx_emoji_version"
    implementation "androidx.emoji:emoji-appcompat:$androidx_emoji_version"
    implementation "androidx.emoji:emoji-bundled:$androidx_emoji_version"
    testImplementation 'junit:junit:4.13.2'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-accessibility:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.4.0'
    androidTestImplementation 'androidx.test.espresso.idling:idling-concurrent:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-idling-resource:3.4.0'
    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}
