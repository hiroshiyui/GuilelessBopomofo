apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    defaultConfig {
        applicationId "org.ghostsinthelab.apps.guilelessbopomofo"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 50
        versionName "1.4.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
        vectorDrawables {
            useSupportLibrary = true
        }
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.18.1"
        }
    }
    ndkVersion '22.1.7171670'

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    task prepareChewing(type: Exec) {
        onlyIf {
            files("$rootDir/app/libs/libchewing/data/dictionary.dat", "$rootDir/app/libs/libchewing/data/index_tree.dat").any { file ->
                !file.exists()
            }
        }
        workingDir("$rootDir/app/libs/libchewing")
        commandLine('./build-android.sh')
    }

    task copyChewingDataFiles(type: Copy) {
        dependsOn 'prepareChewing'
        from("$rootDir/app/libs/libchewing/data") {
            include("dictionary.dat", "index_tree.dat", "pinyin.tab", "swkb.dat", "symbols.dat")
        }
        into("$rootDir/app/src/main/assets")
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    preBuild.dependsOn('copyChewingDataFiles')

    clean.doLast {
        file("$rootDir/app/src/main/assets/dictionary.dat").delete()
        file("$rootDir/app/src/main/assets/index_tree.dat").delete()
        file("$rootDir/app/src/main/assets/pinyin.tab").delete()
        file("$rootDir/app/src/main/assets/swkb.dat").delete()
        file("$rootDir/app/src/main/assets/symbols.dat").delete()
    }
}

dependencies {
    def androidx_appcompat_version = '1.3.1'
    def androidx_emoji_version = '1.2.0-alpha03'

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "androidx.appcompat:appcompat-resources:$androidx_appcompat_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.github.google:flexbox-layout:2.0.1'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.5.21'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.1-native-mt'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1-native-mt'
    implementation "androidx.emoji:emoji:$androidx_emoji_version"
    implementation "androidx.emoji:emoji-appcompat:$androidx_emoji_version"
    implementation "androidx.emoji:emoji-bundled:$androidx_emoji_version"
    testImplementation 'junit:junit:4.13.2'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-accessibility:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.4.0'
    androidTestImplementation 'androidx.test.espresso.idling:idling-concurrent:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-idling-resource:3.4.0'
    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}
